<?xml version="1.0" encoding="UTF-8"?>
<!--
Copyright (C) 2021 Bengt Martensson.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see http://www.gnu.org/licenses/.
-->
<xsl:stylesheet version="1.0"
                    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                    xmlns:html="http://www.w3.org/1999/xhtml"
                    xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                    xsi:schemaLocation="http://www.harctoolbox.org/RemoteLocator http://www.harctoolbox.org/schemas/remotelocator-0.1.xsd"
                    xmlns:rl="http://www.harctoolbox.org/RemoteLocator">

    <xsl:output method="html"/>

    <!--xsl:template match="@*|node()">
        <xsl:copy>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template-->

    <xsl:template match="/rl:remotedatabase">
        <html>
            <head>
                <title>
                    <!--xsl:text>IRP protocols version </xsl:text-->
                    <xsl:value-of select="@title"/>
                </title>
                <style>
                    <xsl:attribute name="type">text/css</xsl:attribute>
                    div.toc-line {
                    margin-left: 2em;
                    font-style: italic;
                    background: LightGrey;
                    }
                    div.manufacturer {
                    padding-top: 1ex;
                    background: LightGray;
                    }
                    div.manufacturer-title {
                    font-size: 120%;
                    font-weight: bold;
                    }
                    div.deviceClass {
                    margin-left: 2em;
                    }
                    div.deviceClass-title {
                    font-size: 100%;
                    font-weight: bold;
                    }
                    div.remoteLink {
                    padding-left: 2em;
                    background: MiddleGray;
                    }
                    span.comment {
                    font-style: italic;
                    padding-left: 4em;

                    }
                    span.kind {
                    font-size: 80%;
                    font-style: italic;
                    margin-left: 1em;
                    }
                    span.kind::before {
                    content: "(";
                    }
                    span.kind::after {
                    content: ")";
                    }
                    span.protocol {
                    font-size: 80%;
                    font-weight: bold;
                    margin-left: 1em;
                    }
                    span.protocol::before {
                    content: "protocol=";
                    }

                    span.device {
                    font-size: 80%;
                    margin-left: 1em;
                    }
                    span.device::before {
                    content: "device=";
                    }

                    span.subdevice {
                    font-size: 80%;
                    margin-left: 1em;
                    }
                    span.subdevice::before {
                    content: "subdevice=";
                    }
                </style>
            </head>
            <body>
                <h1>
                    <!--xsl:text>List of Irp Protocols, version </xsl:text-->
                    <xsl:value-of select="@title"/>
                </h1>
                <h2>
                <xsl:text>Generated by </xsl:text>
                    <xsl:value-of select="@creatingUser"/>
                    <xsl:text> on </xsl:text>
                    <xsl:value-of select="@creationDate"/>
                </h2>

                <div>
                    <xsl:attribute name="class">toc</xsl:attribute>
                    <h3>Manufacturer links:</h3>
                    <xsl:apply-templates select="rl:manufacturer" mode="toc"/>
                </div>
                <xsl:apply-templates select="rl:manufacturer"/>
            </body>

        </html>
    </xsl:template>

    <xsl:template match="rl:manufacturer" mode="toc">
        <div>
            <xsl:attribute name="class">toc-line</xsl:attribute>
            <a>
                <xsl:attribute name="href">
                    <xsl:text>#</xsl:text>
                    <xsl:value-of select="@name"/>
                </xsl:attribute>
                <xsl:value-of select="@name"/>
            </a>
        </div>
    </xsl:template>

    <xsl:template match="rl:manufacturer">
        <div>
            <xsl:attribute name="class">
                <xsl:value-of select="local-name()"/>
            </xsl:attribute>
            <xsl:attribute name="id">
                <xsl:value-of select="@name"/>
            </xsl:attribute>
            <div>
                <xsl:attribute name="class">
                    <xsl:value-of select="local-name()"/>
                    <xsl:text>-title</xsl:text>
                </xsl:attribute>
                <xsl:value-of select="@name"/>
            </div>
            <xsl:apply-templates select="rl:deviceClass"/>
        </div>
    </xsl:template>

    <xsl:template match="rl:deviceClass">
        <div>
            <xsl:attribute name="class">
                <xsl:value-of select="local-name()"/>
            </xsl:attribute>
            <xsl:attribute name="id">
                <xsl:value-of select="../@name"/>
                <xsl:text>$</xsl:text>
                <xsl:value-of select="@name"/>
            </xsl:attribute>
            <div>
                <xsl:attribute name="class">
                    <xsl:value-of select="local-name()"/>
                    <xsl:text>-title</xsl:text>
                </xsl:attribute>
                <xsl:text>Device Class: </xsl:text>
                <xsl:value-of select="@name"/>
            </div>
            <xsl:apply-templates select="rl:remoteLink"/>
        </div>
    </xsl:template>

    <xsl:template match="rl:remoteLink">
        <div>
            <xsl:attribute name="class">
                <xsl:value-of select="local-name()"/>
            </xsl:attribute>
            <span>
                <xsl:attribute name="class">
                    <xsl:text>url</xsl:text>
                </xsl:attribute>
                <xsl:attribute name="title">
                    <xsl:text>Click to download.</xsl:text>
                </xsl:attribute>
                <a>
                    <xsl:attribute name="href">
                        <xsl:value-of select="@url"/>
                    </xsl:attribute>
                    <xsl:attribute name="target">
                        <xsl:text>downloads</xsl:text>
                    </xsl:attribute>
                    <xsl:value-of select="@name"/>
                </a>
            </span>
            <xsl:apply-templates select="@kind"/>
            <xsl:apply-templates select="@protocol"/>
            <xsl:apply-templates select="@device"/>
            <xsl:apply-templates select="@subdevice"/>
            <xsl:apply-templates select="@comment"/>

        </div>
    </xsl:template>

    <xsl:template match="@protocol">
        <span>
            <xsl:attribute name="class">protocol</xsl:attribute>
            <xsl:value-of select="."/>
        </span>
    </xsl:template>

    <xsl:template match="@device">
        <span>
            <xsl:attribute name="class">device</xsl:attribute>
            <xsl:value-of select="."/>
        </span>
    </xsl:template>

    <xsl:template match="@subdevice">
        <span>
            <xsl:attribute name="class">subdevice</xsl:attribute>
            <xsl:value-of select="."/>
        </span>
    </xsl:template>

    <xsl:template match="@kind">
        <span>
            <xsl:attribute name="class">kind</xsl:attribute>
            <xsl:value-of select="."/>
        </span>
    </xsl:template>

    <xsl:template match="@comment">
        <span>
            <xsl:attribute name="class">
                <xsl:text>comment</xsl:text>
            </xsl:attribute>
            <xsl:value-of select="."/>
        </span>
    </xsl:template>

    <xsl:template match="@url">

    </xsl:template>
</xsl:stylesheet>
